
    model {

  #---------------------------------------------
  # Model for population dynamics in each region
  #---------------------------------------------

  for (j in 1:nstrata){
  
    slope[j] ~ dnorm(0,100)
    
    for (y in 1:nyear){

      logX[j,y] <- slope[j] * (y-1)
      X[j,y] <- exp(logX[j,y])

    }

  } # j

  # *********************************************************
  # Model for breeding origins of migrants arriving at each station
  # *********************************************************
  
  sigma_rho ~ dunif(0,2)
  tau_rho <- pow(sigma_rho,-2)
  
  # Model fluctuations in numbers of migrants from each stratum in each year
  for (s in 1:nstation){


    for (j in 1:nstrata){

    	rho[j,s] ~ dlnorm(log(0.2)-0.5*4,1/4)

      for (y in 1:nyear){
      
         M_star[j,s,y] ~ dlnorm (log(X[j,y] * rho[j,s]), tau_rho)
         M[j,s,y] <- M_star[j,s,y] * rho_fix[j,s]
      }
      
    }

  }

  # Model for known origins of migrating birds
  for (s in 1:nstation){
  
    for (y in 1:nyear){

      # Total number of migrants in year [y] at station [s]
      sumM[s,y] <- sum(M[1:nstrata,s,y])
      #T[s,y] ~ dlnorm(log(sumM[s,y]),tau_rho)
      T[s,y] <- sum(M[1:nstrata,s,y])
      
      # Proportion of birds from each stratum
      for (j in 1:nstrata){
        p[j,s,y] <- M[j,s,y]/sumM[s,y]
      }

      # Multinomial likelihood for observed breeding origins in sample of birds
      N_origin[1:nstrata,s,y] ~ dmulti(p[1:nstrata,s,y],N_station_sampled[s,y])

    } # y
  } # s

  # *********************************************************
  # Model counts of migrants arriving at each station, within each season
  # *********************************************************

  sigma_stationday ~ dunif(0,2)
  tau_stationday <- pow(sigma_stationday,-2)

  # Daily overdispersion in counts at each site (e.g., due to daily weather, stopover behavior, etc.)
  for (s in 1:nstation){

    migration_phenology_mean[s] ~ dunif(1,360)

    # This prior states there is a high probability the "migration period" at the station is between 5 and 15 days in duration
    migration_phenology_sd[s] ~ dlnorm(log(9)-0.5/25,25) 
    migration_phenology_tau[s] <- pow(migration_phenology_sd[s],-2)
    

  }

  for (i in 1:nobs){

    # Distribute migrants throughout the season
    f[i] <-  exp(logdensity.norm(day[i], migration_phenology_mean[station[i]], migration_phenology_tau[station[i]]))
    mu[i] <- log(f[i]) + log(T[station[i],year[i]])
    
    # Add daily overdispersion
    log_lambda[i] ~ dnorm(mu[i] + log(net_hrs[i]), tau_stationday)
    count[i] ~ dpois(exp(log_lambda[i]))

    # Other useful quantities
    expected_count[i] <- exp(mu[i] + log(net_hrs[i]) + 0.5/tau_stationday)
    daily_index[i] <- exp(mu[i] + 0.5/tau_stationday)

    # *********************************************************
    # Simulate counts for posterior predictive checking
    # *********************************************************

    # Simulate new daily counts
    sim_log_lambda[i] ~ dnorm(mu[i] + log(net_hrs[i]), tau_stationday)
    sim_count[i] ~ dpois(exp(sim_log_lambda[i]))

  }


}
